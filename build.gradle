version = '0.0.1-SNAPSHOT'

buildscript {
    ext {
        versions = [
                'pegasus': '2.2.5',
                'checkstyle': '6.6'
        ]
        deps = [
            'pegasus': [
                'data'                 : 'com.linkedin.pegasus:data:' + versions.pegasus,
                'restliClient'         : 'com.linkedin.pegasus:restli-client:' + versions.pegasus,
                'restliServer'         : 'com.linkedin.pegasus:restli-server:' + versions.pegasus,
                'restliGuiceBridge'    : 'com.linkedin.pegasus:restli-guice-bridge:'+versions.pegasus,
                'r2Jetty'              : 'com.linkedin.pegasus:r2-jetty:' + versions.pegasus,
                'd2'                   : 'com.linkedin.pegasus:d2:' + versions.pegasus,
                'gradleplugins'        : 'com.linkedin.pegasus:gradle-plugins:'+versions.pegasus
            ],
            'guice': 'com.google.inject:guice:4.0',
            'json': 'com.googlecode.json-simple:json-simple:1.1.1',
            'logback': 'ch.qos.logback:logback-classic:1.1.3',
            'checkstyle': 'com.puppycrawl.tools:checkstyle:'+versions.checkstyle
        ]
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath deps.pegasus.gradleplugins
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    sourceCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
        mavenLocal()
    }

    configurations {
        all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }

    afterEvaluate {
        if (project.plugins.hasPlugin('pegasus')) {
            dependencies {
                dataTemplateCompile deps.pegasus.data
                restClientCompile deps.pegasus.restliClient
            }
        }
    }
}